package CovidSchools::SchoolScraper::durhamDSB;

use 5.006;
use strict;
use warnings;
use Text::CSV;

use base 'CovidSchools::SchoolScraper';


sub new {
    my $class = shift;
    return $class->SUPER::new(
	DISTRICT => 'Durham DSB',
	URL      => 'https://docs.google.com/spreadsheets/d/1YBn91VaM5IqGhJ_OvShCIpoohIRc2yFI0OwYzBaEe20/export?format=csv&gid=0',
	);
}

#completely override scrape method because it is a google doc in csv, not an HTML file
sub scrape {
    my $self = shift;

    #pretend not to be a robot, because some schoolboards are blocking
    my $ua   = LWP::UserAgent->new(
	agent=>'Mozilla/5.0 (X11; Ubuntu; Linux x86_64; rv:62.0) Gecko/20100101 Firefox/62.0');
    
    my $res  = $ua->get($self->url);
    unless ($res->is_success) {
	$self->error("mirroring of ".$self->district." failed: ".$res->message);
	return;
    }

    # parse contents of mirrored file
    my $content = $self->{raw_content} = $res->content;
    my $array_of_array = Text::CSV::csv(in=>\$content);

    my (%schools,@fields);
    for my $row (@$array_of_array) {
	unless ($self->{parsed_headers}) { # first row
	    $self->{parsed_headers} = $row;
	    @fields                 = @$row;
	    shift @fields;
	    next;
	}
	    
	my ($school,@data) = @$row;
	next unless $school;
	@{$schools{$school}}{@fields} = @data;

    }
    $self->{schools} = \%schools;
}

# this does nothing because there is no HTML table
sub csv {
    my $csv = shift->{raw_content};
    $csv    =~ s/,\s*$//gm;  # remove pesky trailing commas
    $csv;
}

1;
